def isGradle7 = gradle.gradleVersion.startsWith("7")
if (isGradle7) {

    /*Gradle7下的子项目仓库声明*/
    dependencyResolutionManagement {
        repositoriesMode.set(RepositoriesMode.PREFER_SETTINGS)
        repositories {
            google()
            mavenLocal()
            mavenCentral()
            maven { url 'https://jitpack.io' }
        }
    }

}

rootProject.name = "P2M-Example"

include ':lib-http'
include ':lib-common'
//include ':p2m-plugin'
//project(':p2m-plugin').projectDir = new File("../p2m-plugin")
//includeBuild '../p2m-plugin' // for debug ./gradlew assembleDebug -Dorg.gradle.debug=true --scan -s

buildscript {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        /*同时需要声明build.gradle中声明的插件依赖*/
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$KT_VERSION"       // Kotlin支持1.3.20+
        classpath "com.android.tools.build:gradle:$AGP_VERSION"                 // AGP支持3.4.0+
        /*P2M*/
        classpath "com.github.wangdaqi77.P2M:p2m-plugin:$P2M_VERSION"           // P2M插件
    }
}

apply plugin: "p2m-android"

p2m {

    app {                                   // 声明app壳，至少声明一个，可声明多个
        include(":app") {                   // 声明project描述
            projectDir = new File("./app")  // 声明project文件夹路径，如project文件夹路径与settings.gradle同一层级可不用配置
        }
        dependencies {                      // 声明模块依赖项，可依赖多个
            module("Account")
            module("Main")
        }
    }

    app {                                   // 可以声明多个app壳
        include(":app-b")                   // 声明project描述
        dependencies {                      // 声明模块依赖项，可依赖多个
            module("Account")
            module("Main")
        }
    }

    module("Account") {                     // 声明Account模块
        include(":module-account")          // 声明project描述

        groupId = "com.repo"                // 组，默认值模块名。用于发布模块到仓库或者使用仓库中的模块
        versionName = "0.0.1"               // 版本，默认值unspecified。用于发布模块到仓库或者使用仓库中的模块

        useRepo = false                     // 使用远程仓库开关，默认false。true表示使用仓库，false表示使用源码
        runApp = false                      // 运行app开关，默认值false，true表示可以运行app，false表示作为模块，applicationId等配置在./projectPath/build.gradle中的p2mRunAppBuildGradle{}
    }

    module("Main") {
        include(":module-main")
        dependencies {
            module("Account")
            module("Mall")
        }

        groupId = "com.repo"
        versionName = "0.0.1"

        useRepo = false
        runApp = false
    }

    module("Mall") {
        include(":module-mall")
        dependencies {
            module("Account")
        }

        groupId = "com.repo"
        versionName = "0.0.1"

        useRepo = false
        runApp = false
    }

//    p2mMavenRepository {                        // 声明maven仓库, 默认rootProjectPath/repo
//        url = "your maven repository url"       // 仓库地址
//        credentials {                           // 认证信息
//            username = "your user name"
//            password = "your password"
//        }
//    }

}

